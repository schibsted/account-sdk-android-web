apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField('String', 'VERSION_NAME', "\"${project.version}\"")
        }

        debug {
            buildConfigField('String', 'VERSION_NAME', "\"${project.version}\"")
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/java'
    test.java.srcDirs += 'src/test/java'
}

ext {
    okHttpVersion = "4.9.1"
    espressoVersion = "3.4.0"
    mockkVersion = "1.10.5"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'

    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    api "com.squareup.okhttp3:okhttp:${okHttpVersion}"

    api "androidx.browser:browser:1.4.0"
    implementation 'com.nimbusds:nimbus-jose-jwt:9.5'

    //Logging
    implementation "com.jakewharton.timber:timber:$timber_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:${mockkVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"

    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
    testImplementation "androidx.test.espresso:espresso-intents:${espressoVersion}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation project(":testsupport")
    testImplementation 'org.json:json:20180813'
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed"
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact(sourceJar)

                artifactId = 'account-sdk-android-web'
                groupId = 'com.schibsted.account'
                version = project.version

                pom {
                    name = 'Schibsted account Android SDK'
                    description = "Schibsted account Android SDK ${project.version}"
                    url = 'https://github.com/schibsted/account-sdk-android-web'

                    scm {
                        connection = 'scm:git:https://github.com/schibsted/account-sdk-android-web.git'
                        url = 'https://github.com/schibsted/account-sdk-android-web'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'schibsted-account'
                            name = 'Schibsted account'
                            email = 'schibstedaccount@schibsted.com'
                        }
                    }
                }
            }
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier "sources"
}

static String base64Decode(String envVar) {
    def value = System.getenv(envVar)
    if (value == null) {
        return null
    }

    return new String(value.decodeBase64())
}

signing {
    useInMemoryPgpKeys(System.getenv('SIGNING_KEY_ID'), base64Decode('SIGNING_KEY'), base64Decode('SIGNING_PASSWORD'))
    sign publishing.publications
}
